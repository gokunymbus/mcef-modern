plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version + "+mc" + project.minecraft_version + ".jcef" + project.jcef_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

loom {
    mods {
        "mcef-modern" {
            sourceSet sourceSets.main
        }
        testmod {
            sourceSet sourceSets.testmod
        }
    }

    runs {
        testmodClient {
            client()
            name = "Test Mod Client"
            source sourceSets.testmod
        }
    }

    runConfigs.configureEach {
        ideConfigGenerated = true
    }
}

repositories {
    maven {
        url "https://maven.terraformersmc.com/releases/"
    }
}

dependencies {
    // Fabric Loader
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // Mod Menu
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

    // JCEF
    modImplementation "me.friwi:jcefmaven:${jcef_version}"

    testmodImplementation sourceSets.main.output
    testmodAnnotationProcessor sourceSets.main.output
}

processResources {
    inputs.property "version", project.version
    inputs.property "fabric_version", fabric_version
    inputs.property "minecraft_version", minecraft_version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version,
                "fabric_version": inputs.properties.fabric_version,
                "minecraft_version": inputs.properties.minecraft_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        String mavenToken = System.getenv("MAVEN_TOKEN")
        if (mavenToken) {
            maven {
                name "dimaskama"
                url "https://maven.dimaskama.net/releases"
                credentials {
                    username 'main-write'
                    password mavenToken
                }
            }
        }
    }
}

def modrinthToken = System.getenv("MODRINTH_TOKEN")
if (modrinthToken) {
    modrinth {
        token = modrinthToken
        projectId = "mcef-modern"
        versionNumber = project.mod_version
        versionType = "release" // This is the default -- can also be `beta` or `alpha`
        changelog = file('changelog.md').text
        uploadFile = remapJar
        gameVersions = ["1.21.10"] // Must be an array, even with only one version
        dependencies { // A special DSL for creating dependencies
            // scope.type
            // The scope can be `required`, `optional`, `incompatible`, or `embedded`
            // The type can either be `project` or `version`
            required.project "fabric-api"
        }
    }
}
